plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
}

allprojects {
    version '1.4.1'
    group 'com.void01.bukkit'

    repositories {
        mavenCentral()
        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            url 'https://maven.void01.com/repository/bukkit-plugin/'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'

        options.encoding = 'UTF-8'
        options.compilerArgs.add('-parameters')
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
            javaParameters = true
        }
    }

    dependencies {
        compileOnly fileTree(dir: new File(rootDir, 'libs'), includes: ['*jar'])

        compileOnly 'org.bukkit:bukkit:1.13.2-R0.1-SNAPSHOT'
        compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
        compileOnly 'net.md-5:bungeecord-chat:1.16-R0.4'

        compileOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

        compileOnly 'com.void01.bukkit:void-framework-bukkit-api:latest' changing true
        compileOnly 'com.void01.bukkit:void-framework-bukkit-api-legacy:latest' changing true
        compileOnly 'com.void01.bukkit:void-framework-bukkit-common:latest' changing true
        compileOnly 'com.void01.bukkit.thirdparty:germ-plugin:4.3.6'
    }

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

tasks.register('cleanBuildCore', GradleBuild) {
    //noinspection GroovyAssignabilityCheck
    tasks = [':core:clean', ':core:build']
}

tasks.register('cleanPublishAPI', GradleBuild) {
    //noinspection GroovyAssignabilityCheck
    tasks = [':api:clean', ':api:publish']
}